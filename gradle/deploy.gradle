apply plugin: "maven"

ext {
    buildNumber = System.env.BUILD_NUMBER ?: "0"
    ciSystem = System.env.CI_SYSTEM
    commit = System.env.GIT_COMMIT
    branch = System.env.GIT_BRANCH
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("tempusRepo")) {
                repository(url: project.tempusRepo) {
                    if (project.hasProperty("tempusUsername") && project.hasProperty("tempusPassword")) {
                        authentication(userName: project.tempusUsername, password: project.tempusPassword)
                    }
                }
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.archivesBaseName
                    description = project.description
                    packaging = "jar"
                    url = project.url

                    scm {
                        url = "https://github.com/$project.organization/$project.name"
                        connection = "scm:git:https://github.com/$project.organization/${project.name}.git"
                        developerConnection = "scm:git:git@github.com:$project.organization/${project.name}.git"
                    }

                    issueManagement {
                        system = "GitHub Issues"
                        url = "https://github.com/$project.organization/$project.name/issues"
                    }
                    
                    licenses {
                        license {
                            name = "MIT License"
                            url = "http://opensource.org/MIT"
                            distribution = "repo"
                        }
                    }
                }
            }
        }
    }
}
